plugins {
    id 'eclipse'
    id 'groovy'
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'org.ajoberstar.grgit' version '2.2.1'
    id 'com.github.ben-manes.versions' version '0.51.0'
}

version = 'v21.5-SNAPSHOT'

java {
    sourceCompatibility = 17
    targetCompatibility = 17

    withSourcesJar()
}

//tasks.withType(JavaCompile) {
//    options.compilerArgs += ['-Xlint:deprecation']
//}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

def repo = org.ajoberstar.grgit.Grgit.open(currentDir: project.rootDir)

jar {
	
    archiveBaseName = 'graphql-jpa'
    version = project.version
    manifest {
        attributes 'Implementation-Revision': repo.head().getAbbreviatedId(8),
                   'Implementation-Branch': repo.branch.getCurrent().getName(),
                   'Implementation-Remote-URL': repo.remote.list().first().url,
				   'Built-By': System.properties['user.name']
    }
}

repositories {
    mavenCentral()
}

configurations {
    provided
    compile.extendsFrom provided
}

dependencies {
    implementation 'com.graphql-java:graphql-java:22.0'
    implementation 'com.graphql-java:graphql-java-extended-scalars:22.0'
    implementation 'jakarta.transaction:jakarta.transaction-api:2.0.1'
    implementation 'net.bytebuddy:byte-buddy:1.14.14'
    implementation 'net.bytebuddy:byte-buddy-agent:1.14.14'
    implementation 'jakarta.servlet:jakarta.servlet-api:6.0.0'
    implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'
    implementation 'jakarta.platform:jakarta.jakartaee-api:10.0.0'

    testImplementation "org.springframework.boot:spring-boot-starter:3.2.5"
    testImplementation "org.springframework.boot:spring-boot-starter-test:3.2.5"
    testImplementation "org.springframework.boot:spring-boot-starter-data-jpa:3.2.5"
    testImplementation "org.springframework.boot:spring-boot-starter-web:3.2.5"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'org.spockframework:spock-spring:2.3-groovy-4.0'
    testImplementation 'org.apache.groovy:groovy-all:4.0.21'
    testImplementation 'cglib:cglib-nodep:3.3.0'
    testImplementation 'org.objenesis:objenesis:3.3'

    testRuntimeOnly "com.h2database:h2:1.4.190"
}

publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java
			groupId 'com.crygier'
    		artifactId 'graphql-jpa'
    		version project.version
		}
	}
}

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

wrapper {
    gradleVersion = '8.0.2'
}
