plugins {
    id 'eclipse'
    id 'groovy'
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'org.ajoberstar.grgit' version '2.2.1'
}

version = 'v18.6-SNAPSHOT'

java {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    withSourcesJar()
}

def repo = org.ajoberstar.grgit.Grgit.open(currentDir: project.rootDir)

jar {
	
    archiveBaseName = 'graphql-jpa'
    version = project.version
    manifest {
        attributes 'Implementation-Revision': repo.head().getAbbreviatedId(8),
                   'Implementation-Branch': repo.branch.getCurrent().getName(),
                   'Implementation-Remote-URL': repo.remote.list().first().url,
				   'Built-By': System.properties['user.name']
    }
}

repositories {
    mavenCentral()
}

configurations {
    provided
    compile.extendsFrom provided
}

dependencies {
    implementation 'com.graphql-java:graphql-java:18.6'
    implementation 'com.graphql-java:graphql-java-extended-scalars:18.3'
    implementation 'javax.transaction:javax.transaction-api:1.2'
    implementation 'net.bytebuddy:byte-buddy:1.12.22'
    implementation 'net.bytebuddy:byte-buddy-agent:1.12.22'
    implementation 'javax.servlet:javax.servlet-api:4.0.1'
    implementation 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final'
    implementation 'javax:javaee-api:8.0.1'

    testImplementation "org.springframework.boot:spring-boot-starter:2.7.8"
    testImplementation "org.springframework.boot:spring-boot-starter-test:2.7.8"
    testImplementation "org.springframework.boot:spring-boot-starter-data-jpa:2.7.8"
    testImplementation "org.springframework.boot:spring-boot-starter-web:2.7.8"
    testImplementation 'junit:junit:4.11'
    testImplementation 'org.spockframework:spock-core:1.1-groovy-2.4'
    testImplementation 'org.spockframework:spock-spring:1.1-groovy-2.4'
    testImplementation 'org.codehaus.groovy:groovy-all:2.4.4'
    testImplementation 'cglib:cglib-nodep:3.1'
    testImplementation 'org.objenesis:objenesis:2.1'

    testRuntimeOnly "com.h2database:h2:1.4.190"
}

publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java
			groupId 'com.crygier'
    		artifactId 'graphql-jpa'
    		version project.version
		}
	}
}

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

wrapper {
    gradleVersion = '8.0.2'
}
